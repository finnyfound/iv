~version: "2.0"

##r should not be pic https://pik.ua/news/url/u-hersoni-rozpochav-robotu-pasportnij-servis
##r missing figcaption https://pik.ua/news/url/vidstavka-pid-tiskom-chi-za-vlasnim-bazhannjam
site_name: "ПІК"
body: //div[has-class("report")]

title: $body//div[has-class("report__title")]/h1

$time: $body//div[has-class("report-time")]
@replace("Сьогодні", "today", "i"): $time
@replace("вчора", "yesterday", "i"): $time
@datetime: $time
published_date: $@

@remove: $body/div[has-class("report__top")]

<div>: //a[.//iframe or .//div[has-class("fb-video")]]

@background_to_image: $body/div[has-class("media-item")]//span
cover: $body/div[has-class("media-item")]//img
@prepend_to($body): $body/div[has-class("media-item") and not(.//img)]

@remove: $body/div[has-class("report__bottom")]
# https://pik.ua/news/url/855-za-kubometr-kudi-jdut-vashi-koshti-za-gaz-kalkuljator
@unsupported: $body//input

<related>: $body/../next-sibling::div[has-class("post--video")]
$related: $@
# comment diffonly {
<h1>: $related/div[has-class("title-section")]
#}
@append_to($body): $related

# no media https://pik.ua/news/url/do-1000-dolariv-za-dopis-facebook-pokazav-skilki-ukrajinski-politiki-vklali-v-reklamu-dlja-ssha

# https://pik.ua/news/url/vidstavka-pid-tiskom-chi-za-vlasnim-bazhannjam
<figure>: $body//div[has-class("article-img")]
$figure: $@
<figcaption>: $figure//div[has-class("article-img__info")]
<cite>: $@//div[has-class("article-img__source")][1]
@after_el("./ancestor::figure"): $figure//div[has-class("article-img__source")]

# https://pik.ua/news/url/u-chongari-planujut-pereviriti-majno-biglogo-do-krimu-ukrajinskogo-chinovnika
<figure>: $body//*[has-class("wp-caption")]
@wrap_inner(<figcaption>): $@
#@wrap_inner(<cite>): $@
@before_el("./ancestor::figcaption"): $@//img

##r https://pik.ua/news/url/jak-provesti-vihidni-u-hersoni9747
@set_attr(href, @src): $body//img[@height>2000 and @width < 1000]

#  ___  _                   
# |__ \| |                  
#    ) | |_ _ __ _   _  ___ 
#   / /| __| '__| | | |/ _ \
#  |_| | |_| |  | |_| |  __/
#  (_)  \__|_|   \__,_|\___|

?true
@replace("\\s", " "): //text()
# https://pik.ua/news/url/dveri-do-kabinetu-golovnogo-likarja-kahovskoji-tsrl-dovelosja-vilamuvati
# comment diffonly {
@after(<br>): $body//blockquote//p
#}

# ULTIMATE PARSER 9000

$problems: $body
@while( $problems ) {
  $test: $body//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or self::iframe or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)]]
 
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br or ancestor::ul or ancestor::li]
 
  @split_parent: $problems
}