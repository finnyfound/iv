~version: "2.0"

# comment diffonly {
@replace("\\s", " "): //text()
# }

main: //main[has-class("post")]
body: $main//div[@id="articleBody"]

@background_to_image: $body//*[@id="articleThumb"]
cover: $@

author: $body//div[@id="authorInfo"]//a[@rel="author"]
# comment diffonly {
kicker: $main/h1//span[has-class("headline")]
title: $main/h1/text()
# } else {
#title: $main/h1
#}
# diffonly {
author_url: $author/@href
# }

@remove: $body//noscript


@set_attr(src, @data-src): //iframe

<slideshow>: $body//p[contains(@style, "text-align: center;")][./a/img]
$slideshow: $@
<figcaption>: $slideshow/em
@combine: $slideshow/figcaption/following-sibling::*


<figcaption>: $slideshow/next-sibling::p[contains(@style, "text-align: center;")]
@append_to("./prev-sibling::slideshow"): $@

@remove: $body//slideshow/next-sibling::a[string-length(text()) < 2][contains(@rel, "attachment")]
@combine: $body//slideshow[not(./figcaption)]/next-sibling::slideshow


<figure>: $body//p[contains(@style, "text-align: center;")][./iframe and not(./iframe[2])]
$slideshow_frame: $@
<figcaption>: $slideshow_frame/next-sibling::p[contains(@style, "text-align: center;")]
@append_to("./prev-sibling::figure"): $@

@remove: $body//*[has-class("social")]
@remove: $body/*[has-class("underImage")]
@remove: $body/div[@id="comments"]
@remove: $body//div[@id="authorInfo"]
@remove: $body//*[@id="highlitesAdsDown"]
@remove: $body//*[has-class("heureka_widget")]

<table>: $body//*[has-class("StoreWidget")]//span[has-class("widget-rozmer")]
$widget: $@

# Srcset

@set_attr(src, @data-src): //img
@set_attr(srcset, @data-srcset): //img
@replace("(http.+?)(?=\\d+w)(\\d+)w(?:,|$)", "<link src=$1 res=$2>"): $body//img/@srcset
@append(@srcset): $body//img[@srcset]
@html_to_dom: $@
@set_attr(src, .//link[not(../link/@res>@res)]/@src): $body//img[@srcset]

<not_a_table>: $body//table[.//img][@border=0]
$old_table: $@
<figure>: $old_table//tr[1]//td[.//img]
<figcaption>: $old_table//tr[2]//td[contains(@style, "text-align: center")]
@append_to("./ancestor::not_a_table//figure"): $@


@wrap(<td>): $widget/img
<pic>: $@/img
@set_attrs(width, 100, height, 100): $@
# comment diffonly {
@wrap(<a>, href, "./ancestor::a/@href"): $$
# }
@wrap(<td>): $widget/span[has-class("widget-text")]
<p>: $$/span
<b>: $widget//p[has-class("widget-zaklad")]
@wrap_inner(<a>, href, "./ancestor::a/@href"): $@
@wrap(<td>): $widget/span[has-class("typeSystem")]
@wrap_inner(<a>, href, "./ancestor::a/@href"): $@


<footer>: $body//*[has-class("zdroje")]


#  ___  _                   
# |__ \| |                  
#    ) | |_ _ __ _   _  ___ 
#   / /| __| '__| | | |/ _ \
#  |_| | |_| |  | |_| |  __/
#  (_)  \__|_|   \__,_|\___|

?true



@set_attr(src, "./parent::a/@href"): $body//img[./parent::a]

# ULTIMATE PARSER 9000

$problems: $body
@while( $problems ) {
  $test: $body//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or (self::iframe and not(./ancestor::figure)) or (self::video and not(./ancestor::figure)) or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)] or self::h1 or self::h2 or self::h3 or self::h4 or self::h5]
 
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
 
  @if( $problems ) {
    $parent: $problems/parent::*
    @detach: $parent/node()
    @set_attr("merge", $index): $@
    <remove>: $parent
    @after_el("./parent::*"): $problems
    @combine: $body//self::*[prev-sibling::*/@merge = @merge]
  }
}