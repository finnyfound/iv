~version: "2.0"

?path_not: /*pics/*.*

article: //main/article
body: $article//div[has-class("article-body--article") or has-class("article-body--celeb")]

title: $article//header//h1
author: $article//div[has-class("byline-social__author-name")]

# Lead paragraph
<h5>: $body/h3[1]

# Cover
featured_media: $article//div[has-class("featured-media")]
cover: $featured_media//figure[has-class("featured-media__image")]
gallery: $featured_media//div[has-class("featured-media__gallery-items")]

#kicker: $article//div[has-class("article-header__eyebrow-item--taxonomy")]

<iframe>: $gallery//a[has-class("featured-media__view-gallery")]
$gallery_iframe: $@
@set_attr(src, "./@href"): $gallery_iframe
@inline: $gallery_iframe
$inlined: $@
gallery_left: $inlined//div[has-class("gallery__left")]
<slideshow>: $gallery_left
gallery_left!
<figure>: $gallery_left//div[has-class("gallery__thumbnail")]
$figure_gallery_left

@map( $inlined//div[has-class("gallery__bottom-image-info")]//div ) {
 $caption_gallery: $@
 <figcaption>: $caption_gallery/h2
 @if_not( $@ ) {
   @append(<figcaption>): $caption_gallery
 }
 $figcaption_gallery: $@
 @append(<br>): $figcaption_gallery
 @append_to($figcaption_gallery): $caption_gallery/p[has-class("sub-text-1")]
 <cite>: $caption_gallery/p[has-class("sub-text-2")]
 @append_to("./prev-sibling::figcaption"): $@
 @append_to("./ancestor::html[1]//figure[not(./figcaption)]"): $caption_gallery/figcaption
}

@prepend_to($body): $gallery_left

@match("'jwplayer_(.*?)_", 1): //script[contains(text(), "jwplayer_")]
<video>: $@
@set_attr(src, "https://content.jwplatform.com/manifests/", $@/text(), "\\.m3u8"): $@


#@debug: $article//div[contains(@id, "jwplayer")]

# Related
<related>: $article//div[has-class("read-more")]
@append_to($body)

@wrap(<figure>): $body//img[./ancestor::*/next-sibling::*/em]
<figcaption>: $@/ancestor::*/next-sibling::*/em[string-length(./../text()) = 0]
@append_to("./parent::*/prev-sibling::*/figure"): $@
@unsupported: $body//input

<iframe>: $body//div[has-class("tumblr-post")][@data-href]
@set_attr(src, @data-href): $@
@remove: //img[@width=1]

# @remove: $body//div[has-class("PDS_Poll")]/parent::*[./noscript]
#        _          
#       (_)         
#  _ __  _  ___ ___ 
# | '_ \| |/ __/ __|
# | |_) | | (__\__ \
# | .__/|_|\___|___/
# | |               
# |_|               

?path: /*pics/*.*

gallery_left: //div[has-class("gallery__left")]
<div>: $gallery_left
gallery_left!
<figure>: $gallery_left//div[has-class("gallery__thumbnail")]
$figure_gallery_left

@map( //div[has-class("gallery__bottom-image-info")]//div ) {
 $caption_gallery: $@
 <figcaption>: $caption_gallery/h2
 @if_not( $@ ) {
   @append(<figcaption>): $caption_gallery
 }
 $figcaption_gallery: $@
 @append(<br>): $figcaption_gallery
 @append_to($figcaption_gallery): $caption_gallery/p[has-class("sub-text-1")]
 <cite>: $caption_gallery/p[has-class("sub-text-2")]
 @append_to("./prev-sibling::figcaption"): $@
 @append_to("./ancestor::html[1]//figure[not(./figcaption)]"): $caption_gallery/figcaption
}

body: $gallery_left
published_date: //meta[@property="article:published_date"]/@content
@remove: //div[has-class("gallery__close-thumbnails")]
#@prepend_to($body): //div[has-class("gallery__back-to-linked-post")]

#           _      _     
#          | |    | |    
#   ___ ___| | ___| |__  
#  / __/ _ \ |/ _ \ '_ \ 
# | (_|  __/ |  __/ |_) |
#  \___\___|_|\___|_.__/ 
 
?path: /*celeb/*.*

<table>: $article//div[has-class("celeb-details")]
$celeb_details
<caption>: $celeb_details/h2
<tr>: $celeb_details//div[has-class("celeb-details__item")]
$tr
@append_to("./../.."): $tr
<th>: $tr/dt
<td>: $tr/dd

@prepend_to($body): $celeb_details
@remove: $body//button[has-class("celeb__read-more-button")]

#  ___  _                   
# |__ \| |                  
#    ) | |_ _ __ _   _  ___ 
#   / /| __| '__| | | |/ _ \
#  |_| | |_| |  | |_| |  __/
#  (_)  \__|_|   \__,_|\___|

?true

#@replace("(.*)\\?.*", "$1"): //img/@src
#@replace("^\\s+$", ""): //text()

# ULTIMATE PARSER 9000

$problems: $body
@while( $problems ) {
  $test: $body//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or self::iframe or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)] or self::video]
 
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
 
  @if( $problems ) {
    $parent: $problems/parent::*
    @detach: $parent/node()
    @set_attr("merge", $index): $@
    <remove>: $parent
    @after_el("./parent::*"): $problems
    @combine: $body//self::*[prev-sibling::*/@merge = @merge]
  }
}

?path: /*advertise-with-us/*.*
?path: /*send-us-your-scoops-tips-corrections/*.*
?path: /*privacy-policy/*.*
?path: /*about-us/*.*
?path: /*contact-us/*.*
body!!: null
