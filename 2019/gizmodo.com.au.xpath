~version: "2.1"

  ?not_exists: //meta[@property="og:type"][contains(@content,"article")]

site_name: "Gizmodo Australia"
description!!: ""

  ?exists: //meta[@property="og:type"][contains(@content,"article")]

@unsupported: //form
@unsupported: //div[contains(@class, "Poll")]
<div>: //blockquote[has-class("reddit-card")]
<unsupported>: //script[contains(@src,"vidible") or contains(@src,"stackcommerce") or contains(@src,"complex.com") or contains(@src,"tumblr") or contains(@src,"whistleout")]
@unsupported: //unsupported

title: //h1
body: //section[has-class("main")]
description: ($body//p[normalize-space()][text()])[1]

$mailto: //a[contains(@href,"0")][contains(.//text(), "@")][contains(.//text(), ".")]
@set_attr(href, "mailto:", .//text()): $mailto

@replace("^\\s+$", " "): //text()

@append(<authors>): /html
@if_not ( "//div[has-class(\"meta__author\")]/a" ) {
  @replace(" and ", ", "): //div[has-class("meta__author")]//text()
  @append_to(//authors): $@
}
$authorurl: //div[has-class("meta__author")]/a/@href
@append_to(//authors): //div[has-class("meta__author")]/a
@combine(", "): //authors/a
@if_not( "//authors//text()[contains(.,\",\")]" ) {
  author_url: $authorurl
}
author: //authors

<pic>: //blockquote//img[@class="size_xsmall"]
<b>: //*[self::h1 or self::h2][ancestor::blockquote]
@after(<br>): //blockquote/*[not(next-sibling::ul)][not(next-sibling::ol)][next-sibling::*]
<div>: //aside[.//a][.//img]
@set_attr(class, "review-box"): $@

@before(<hr>): //div[has-class("review-box")]
@after(<hr>): //div[has-class("review-box")]

@replace("(upload)(.+)(\/c)","$1$3"): //a[.//img]/@href

<temp>: (//article//img)[1]
@map("//a[.//img][contains(@href, \"jpg\") or contains(@href, \"jpeg\") or contains(@href, \"png\")]") {
   @set_attr(src, $@/@href): $@//img[not(@class="size_xlarge")]
}
<img>: //temp
@map("//a[.//img][contains(@href, \".gif\")]") {
   @set_attr(src, $@/@href): $@//img
}
@map("//a[.//img][not(contains(@href, \"#\") or contains(@href, \".jpg\") or contains(@href, \".jpeg\") or contains(@href, \".png\") or contains(@href, \".gif\"))]") {
  @set_attr(href, $@/@href): $@//img
}

<figure>: //div[has-class("media_wrap")][not(.//pic)][not(.//div[contains(@class, "review")])]
@wrap(<slideshow>): //figure[not(prev-sibling::figure)][next-sibling::figure]
@while ( "//slideshow/next-sibling::figure" ) {
  @append_to("./prev-sibling::slideshow"): $@
}

@wrap(<figure>): //iframe
<figcaption>: //article//small[ancestor::figure][not(prev-sibling::small or prev-sibling::figcaption)]
<figcaption>: //article//small[prev-sibling::figure or prev-sibling::slideshow]
<figcaption>: //article//small[ancestor::p[not(text()[normalize-space()])][prev-sibling::div[not(.//text()[normalize-space()])][.//figure]]][not(prev-sibling::small or prev-sibling::figcaption)]
@wrap(<span>): //p[.//small][count(.//small)=1]/text()[normalize-space()]
<figcaption>: //article//small[not(prev-sibling::span)][ancestor::p[prev-sibling::figure or prev-sibling::slideshow]][not(prev-sibling::small or prev-sibling::figcaption)] 
<cite>: //article//small[prev-sibling::figcaption]
@append_to("./preceding::figcaption[not(descendant::cite)][1]"): //cite[not(ancestor::figcaption)][1]
@append_to("./preceding::slideshow[not(descendant::figcaption)][1]"): //figcaption[not(ancestor::figure)][not(ancestor::slideshow)][1]
@append_to("./preceding::figure[not(descendant::figcaption)][1]"): //figcaption[not(ancestor::figure)][not(ancestor::slideshow)][1]

@wrap(<i>): //small[not(ancestor::blockquote)]
@before(<br>): $@

<slideshow>: //div[@class="owl-wrapper"]
<figure>: //div[@class="item"]
<figcaption>: //div[@class="content"]
<cite>: //p[@class="source"]
@wrap(<figcaption>): $@
@combine: $@/next-sibling::figcaption

<cite>: //span[@class="attribution"]

@append(<a>): //blockquote[@class="instagram-media"]
@set_attr(href, "./ancestor::blockquote/@data-instgrm-permalink"): //blockquote[@class="instagram-media"]//a

@wrap(<details>): //div[has-class("media_wrap")][.//div[contains(@class, "review")]]
@set_attr(open,""): $@
<summary>: //div[has-class("review-score-title")]
<summary>: //div[has-class("review-specs-h1")]
@wrap_inner(<b>): //summary
<pic>: //div[has-class("review-score-buy-now")]//img
@wrap(<aside>): $@/../..

# @before(<hr>): //div[has-class("media_wrap")][.//div[contains(@class, "review")]]
# @after(<hr>): //div[has-class("media_wrap")][.//div[contains(@class, "review")]]
<h2>: //div[has-class("review-score-title")]
<h2>: //div[has-class("review-specs-h1")]
<h5>: //div[has-class("review-score-h1")]
<h6>: //span[@class="review-score-text"]
@prepend("Gizmodo Score: "): $@

<related>: //div[@id="inner_related_posts"]
@append_to($body): $@
<related>: //div[has-class("referenced-article")]
@append_to($body): $@
@combine: //related/next-sibling::related

@remove: //hr/next-sibling::hr
@remove: //iframe[not(@src)]
@remove: //related//img
@remove: //div[@class="meta"]
@remove: //hr[next-sibling::section]
@remove: //section[not(has-class("main"))]

@remove: //img[@height = 1][@width = 1]

<div>: //slideshow[.//iframe]
<div>: //em[.//*[self::h1 or self::h2 or self::h3]]
# ====================
$problems: $body
@while( $problems ) {
  $test: $body//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or (self::iframe and not(./ancestor::figure)) or (self::video and not(./ancestor::figure)) or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)]]
 
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote[not(contains(@class,"twi") or contains(@class,"instagram") or contains(@class,"fb-"))] or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
 
 @split_parent: $problems
}
# ====================
