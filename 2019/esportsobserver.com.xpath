~version: "2.0"

?path_not: /*my-account.*
!path_not: /*find-a-job.*
!path_not: /*post-a-job.*
!path_not: /*job-dashboard.*
!path_not: /*job/*.*
!path_not: /*contact-us.*
!path_not: /*privacy.*
!path_not: /*cart.*

main: //main
article: $main/article
body: $article/div[has-class("entry-content")]

# Pullquote
<aside>: $article//*[has-class("perfect-pullquote")]
<div>: $@//blockquote

@set_attr(href, "./../self::a/@href"): $article//a/img[not(ends-with(../@href, ".jpg"))][not(ends-with(../@href, ".png"))][not(ends-with(../@href, ".jpeg"))][not(ends-with(../@href, ".gif"))]

<div>: $body//p[.//iframe]
<div>: $body//p[.//video]
#@after_el("./parent::div/parent::blockquote"): $body//blockquote//blockquote

@remove: //noscript/img
@set_attr(src, "./@data-lazy-src"): //img[@data-lazy-src][@data-lazy-type="image"]
@remove: //img[@data-lazy-type="iframe"]

title: $article/header/h1
<figure>: $article/div[has-class("entry-thumb")]
cover
<figcaption>: $@/div[has-class("ccfic")]
<cite>: $@/span[has-class("ccfic-source")]


# Social media
@remove: $body//div[has-class("apss-social-share")]
@remove: $body//*[has-class("tags-links")]
@remove: $body//*[@id="fb_group"]

meta: $article/div[has-class("entry-meta")]
author: $meta//*[has-class("author")]
author_url: $meta//*[has-class("link_twitter")]//a/@href

# Inline related
<span>: $body//*[starts-with(text(), "RELATED")]
$related_title+
<span>: $body//*[starts-with(text(), "related")]
$related_title+
<span>: $body//*[starts-with(text(), "Related")]
$related_title+
@after_el("./.."): $related_title/a
@append_to("./ancestor::*/*[starts-with(text(), 'Related')]/.."): $related_title/next-sibling::*//a
@append_to("./../prev-sibling::*"): $related_title/next-sibling::*/a
@wrap(<related>): $related_title[./prev-sibling::*]
<related>: $related_title/parent::*[./a]

<div>: $body//p[.//related]

# Bottom related
<related>: $main/div[has-class("vmag-related-wrapper")]
@append_to($body)

<pic>: //img[@width=16 or @width=35]
$pic: $@
#@append_to("./../prev-sibling::a"): $pic[./../prev-sibling::a]
#@remove: $pic/../next-sibling::div
@wrap(<sup>): $pic

# Want to get the latest esports business news directly on your device?
$ad1: $body//p[contains(text(), "Want to get the latest esports business news directly on your device?")]
@remove: $ad1/prev-sibling::hr/prev-sibling::*/following-sibling::*

<div>: $body//p[.//img]
<div>: $body//a[./img]
<div>: $body//em[.//img]
<div>: $body//p[.//aside]

<figcaption>: $body//img//ancestor::*/next-sibling::p//em
$figcapt+
<figcaption>: $body//img//ancestor::*/next-sibling::h5
$figcapt+
@wrap(<figure>): $figcapt/prev-sibling::*//img
@wrap(<figure>): $figcapt/ancestor::*[1]/prev-sibling::*//img
@append_to("./ancestor::*[1]/prev-sibling::*//figure"): $figcapt
@append_to("./prev-sibling::*//figure"): $figcapt

#  ___  _                   
# |__ \| |                  
#    ) | |_ _ __ _   _  ___ 
#   / /| __| '__| | | |/ _ \
#  |_| | |_| |  | |_| |  __/
#  (_)  \__|_|   \__,_|\___|

?true
# not diffonly {
@replace("^\\s+$", ""): //text()
# }

# ULTIMATE PARSER 9000

$problems: $body
@while( $problems ) {
  $test: $body//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or self::iframe or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)]]
 
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
 
  @if( $problems ) {
    $parent: $problems/parent::*
    @detach: $parent/node()
    @set_attr("merge", $index): $@
    <remove>: $parent
    @after_el("./parent::*"): $problems
    @combine: $body//self::*[prev-sibling::*/@merge = @merge]
  }
}
