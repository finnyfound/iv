~version: "2.0"

site_name: "inFranken.de"

?not_exists: //main[@itemprop="mainContentOfPage"]//div[has-class("streamset-player")]

article: //article[@id or has-class("post-type-article") or has-class("post")][not(has-class("post-teaser"))]
header: $article/header
footer: $article/footer

body: $article

title: $header//h1
kicker: $title/../*[has-class("post-topline")]
subtitle: $header//div[has-class("post-description")]

@append(<authors>): //head
@append_to(//head/authors): $footer//span[has-class("post-author-name")][./ancestor::div[has-class("post-author")]//div[not(has-class("post-author-image-verlag"))]]
@replace("^\\s*(.*?)\\s*$", "$1"): //head/authors/span/text()
@combine(", "): //head/authors/span[normalize-space()]/next-sibling::span
author: //head/authors

@datetime: $header//time[has-class("post-published")]/@datetime
published_date: $@


<table>: $body//div[has-class("divTable")]
<td>: $@//div[has-class("divTableCell")]
@append_to("./ancestor::table"): $@
<pic>: $@//img[@width<50]

$start_pic: $article//figure[has-class("post-image")]/a/@href
$z: null
$drop: null

@prepend(<slideshow>): $body
$slideshow: $@

@while_not( $drop ) {
  $drop_next: null
  @append(<json>): //head
  $json: $@
  @set_attr(data, "{\"contextUrl\":\"", $start_pic, "\", \"contextLevel\": 1}"): $json
  @urlencode: $json/@data

  @append(<iframe>): $body
  @set_attr(src, "https://www.infranken.de/_fWS/json/layout/1752/getLayout/?fWSin=", $json/@data): $@

  @inline: $$
  $inlined: $@

  @html_to_dom: $inlined/data/html
  $dom: $@
  $start_pic: $@//a[has-class("next-btn")]/@href
  @if( $z ) {
    @if( $dom//a[has-class("next-btn")][@href=./ancestor::html[last()]/head/start_url/@href] ) {
     $drop: ""
    }
  }
  @if_not( $z ) {
  
    $z: ""
    @if_not( $start_pic ) {
      $drop_next: ""
    }
    @append(<start_url>, href, $start_pic): //head
  }
  @if_not( $drop ) {
    <figure>: $inlined//div[has-class("image-holder")]
    $holder: $@
    <figcaption>: $holder/next-sibling::*[has-class("image-text")]
    $caption: $@
    <cite>: $caption//*[has-class("image-author")]
    @append_to("./prev-sibling::*[has-class(\"image-holder\")]"): $caption
    @append_to($slideshow): $holder
  }
  @remove: $inlined
  @remove: $json
  @if( $drop_next ) {
    $drop: ""
  }
}

@if_not( $slideshow/*[2] ) {
  cover!: $slideshow/*
}
@remove: $article//figure[has-class("post-image")]

#@debug: $@//figcaption//*[has-class("more-images")]
#@debug: $@//script[contains(text(), "fcmsJs.contexturl = ")]

@prepend_to($body): $article/aside[has-class("embed-video")]
# TODO get this back
#@unsupported: $article//figcaption//*[has-class("more-images")]

@remove: $article//img[@width=1]

<related>: $article/footer//div[@id="lesensieauch"]
$related: $@
@append(<div>): $related
$omegadiv: $@
@repeat( 5 ) {
  @append_to($omegadiv): ($related//a)[1]
}
@remove: $related//nav
@append_to($body): $related

<div>: $article//aside[has-class("post-hintergruende")]

<cite>: $article//figcaption//span
@set_attr(src, @data-src): $article//img
@remove: $header
@remove: $footer


?exists: //main[@itemprop="mainContentOfPage"]//div[has-class("streamset-player")]

body: //main[@itemprop="mainContentOfPage"]//section[has-class("section")]//article

title: $body//h1

@datetime: $body//time[has-class("post-published")]/@datetime
published_date

@prepend_to($body): //div[has-class("streamset-player")]
@remove: $body//time[has-class("post-published")]
@remove: $body//div[has-class("post-extras")]

?path: /*ueberregional/*bildergalerien/*diashow.*

body!!: null


#  ___  _                   
# |__ \| |                  
#    ) | |_ _ __ _   _  ___ 
#   / /| __| '__| | | |/ _ \
#  |_| | |_| |  | |_| |  __/
#  (_)  \__|_|   \__,_|\___|

?true
@map( //glomex-player ) {
  $player: $@
  $script: //script[contains(text(), ./ancestor::body//glomex-player/@id)]
  @match("player-(.+)", 1): $player/@id
  @match("integrationId:\\s*\"(.*?)\"", 1): $script
  @set_attr(src, "https://player.glomex.com/integration/1.62.0/iframe-player.html?playlistId=", $player/@id, "&integrationId=", $script/text()): $player

  <iframe>: $player
}

$container: //div[has-class("video-container")]
@remove: $container//figure
@wrap(<figure>): $container//iframe
@append_to("./ancestor::div[has-class(\"video-container\")]//figure"): $container//figcaption[has-class("img-text")]
@match("zum video \"(.+)\"", 1, "i"): $$//a
@append_to("./ancestor::figcaption"): $$

# ULTIMATE PARSER 9000

$problems: $body
@while( $problems ) {
  $test: $body//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or (self::iframe and not(./ancestor::figure)) or (self::video and not(./ancestor::figure)) or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)] or self::h1 or self::h2 or self::h3 or self::h4 or self::h5]
 
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
 
  @if( $problems ) {
    $parent: $problems/parent::*
    @detach: $parent/node()
    @set_attr("merge", $index): $@
    <remove>: $parent
    @after_el("./parent::*"): $problems
    @combine: $body//self::*[prev-sibling::*/@merge = @merge]
  }
}