~version: "2.1"


site_name: "Первый канал"

article: //article[has-class("itv-news") or has-class("video-page") or has-class("article")]
@if_not( $article ) {
  article: //article[has-class("gallery-page")]/parent::article
}

?path: /*perehod-na-cifrovoe-veshanie.*
article: //article[has-class("show-main")]
?true

header: $article//header

body: $article

<related>: $body//*[has-class("w_block--with-incuts")]

title: $header//h1[has-class("title")]
title: //div[has-class("simple-header")]
title: //h2[has-class("itv-video-title")]
title: //h2[has-class("title")]
title: //section[has-class("photogallery")]//h2

@datetime: //div[has-class("date")]
published_date: $@

<iframe>: $body//div[has-class("tv1player")][@data-playlist-url]
$tv_players: $@

<h5>: $body//*[has-class("article-lead")]

$announce: $body//div[has-class("w-block__announce")]
@wrap_inner(<b>): $announce//div[has-class("w-announce__text")]
@remove: $announce//div[has-class("w-announce__link-image")]

@map( $tv_players ) {
  $tv_player: $@

  @clone: $tv_player
  $videoid: $@
  <videoid>: $videoid
  $videoid: $@
  @match("video_id=(\\d+)", 1): $videoid/@data-playlist-url
  @append(@data-playlist-url): $videoid
  @append_to(//head): $videoid

  @set_attr(src, @data-playlist-url): $tv_player
  @inline: $tv_player
  $inlined: $@
  @if( $videoid[string-length(text())>0] ) {
    <details>: $inlined/ancestor::div[has-class("player")]/../div[has-class("playlist")]
    $details: $@
    @append(<summary>): $details
    @append("Плейлист"): $@
    @append(<ol>): $details
    $related: $@
    $mbr: $inlined/*[./uid/text() = //head/videoid/text()]/mbr/*
    @clone: $inlined
    $inlined_clone: $@
    @set_attr(disabled, "yep"): $inlined_clone/*[./uid/text() = //head/videoid/text()]/sharing/link
    <li>: $inlined_clone/*/sharing/link
    $li: $@
    @append(<a>): $li
    $a: $@
    @set_attr(href, ./../text()): $a[not(./parent::li[@disabled])]
    #@set_attr(href, ./text()): $a
    @replace(".*", ""): $li/text()
    @append_to("./../sharing/li//a"): $a/../../../title
    @append_to($related): $li
    # diffonly {
    #@remove: $details
    # }
    @remove: $inlined_clone
  }
  
  @if_not( $videoid[string-length(text())>0] ) {
    $mbr: $inlined/*[1]/mbr/*
  }
  $quality: $mbr[1]
  @if( $mbr[./name="ld"] ) {
    $quality: $mbr[./name="ld"]
  }
  @if( $mbr[./name="sd"] ) {
    $quality: $mbr[./name="sd"]
  }
  @if( $mbr[./name="hd"] ) {
    $quality: $mbr[./name="hd"]
  }
    @debug: $quality

  @append_to("./ancestor::xml/..//iframe"): $quality/src
  <figure>: $@/..
  $figure: $@
  <figcaption>: $inlined/item/title
  @append_to($figure): $@
  @append(<video>): $figure
  @set_attr(src, ./../src): $@
  @remove: $inlined
  @remove: $videoid
}

# diffonly {
  @if_not( $cover ) {
    #cover: $body//figure[.//video]
  }
#}

@remove: $body//*[has-class("right-column")]

@remove: //div[has-class("date")]
@remove: $body//div[has-class("news-top-share-buttons")]
@remove: $body//div[has-class("itv-tag-list")]

@remove: $body//div[has-class("tag-container")]
@remove: $body//div[has-class("user_comments")]
@remove: $body//div[@id="user_comments"]

@remove: $body//div[has-class("social")]
@remove: $body//*[has-class("player-feedback")]
@remove: $body//*[has-class("itv-video-watch-more")]

@remove: $body//*[has-class("itv-push-suggest")]
@remove: $header

<div>: $body//script[contains(@src, "1tv.ru/questionary") or contains(text(), "1tv.ru/questionary")]
@unsupported

<blockquote>: $body//div[has-class("w-paragraph-quote")]
$blockquote: $@
# https://instantview.telegram.org/contest/magadanmedia.ru/template18/issue3/
#@background_to_image: $@//span[has-class("img")]
#<pic>: $@
<b>: $blockquote//span[has-class("w-paragraph-quote__title")]
@wrap(<cite>): $@
@append_to("./ancestor::blockquote//cite"): $blockquote//*[has-class("w-paragraph-quote__subtitle")]

<mark>: $body//div[has-class("w__block__note")]


<figure>: $body//div[has-class("w__block__video")]
$block_video: $@
@remove: $block_video//div[@data-role="splash_wrapper"]
<figcaption>: $block_video//div[has-class("w_video_title")]

@combine(", "): $body//div[has-class("authors")]/a/following-sibling::a[1]/self::a

author: $body//div[has-class("authors")]//text()
@remove: $body//div[has-class("authors")]

<figure>: $body//div[has-class("article-illustration")]
$figure: $@
<figcaption>: $figure/span
cover: $figure

<figure>: $body//div[has-class("w__block__illustration")]
$figure: $@
<figcaption>: $figure/div[has-class("w_illustration_title")]
<cite>: $figure/*[has-class("w_illustration_copyright")]
@append_to("./../figcaption"): $@

$info_wrapper: $body//div[has-class("w-block__text-info-wrapper")]
@before(<hr>): $info_wrapper
@after(<hr>): $info_wrapper

<slideshow>: $body//*[has-class("w__block__photo_gallery")]
$slideshow: $@
<figcaption>: $slideshow//div[has-class("w_photo_gallery_title")]
<cite>: $slideshow//div[has-class("photo_producer")]
@append_to("./ancestor::slideshow/figcaption"): $@

<figure>: $slideshow//div[has-class("w_photo_gallery_photos")]/a[has-class("card")]
<figcaption>: $@/div[@data-role="info"]
@html_to_dom: $$/@data-desc
@append_to("./.."): $@

<h5>: $body//div[has-class("closing-message")]

# https://www.1tv.ru/shows/naedine-so-vsemi/vypuski/pamyati-yulii-nachalovoy-naedine-so-vsemi-vypusk-ot-18-11-2013
<related>: $body//div[has-class("w__block__collection") and has-class("type-tiles")]
$related: $@
@append(<div>): $related
$block: $@
@repeat( 5 ) {
  @append_to($block): ($related//div[has-class("w_collection_items")]//a)[1]
}
@remove: $related//div[has-class("w_collection_items")]//a
<h1>: $related/div[has-class("w_collection_title")]

<related>: //*[has-class("itv-read-more")]
$related: $@
<div>: $related//article/h3
@remove: $related//article/div[has-class("itv-news-list__info")]
@append(<div>): $related
$block: $@
@repeat( 5 ) {
  @append_to($block): ($related//article)[1]
}
@remove: $related/article
@append_to($body): $related

@unsupported: $body//div[has-class("w__block__voting")]
@set_attr(src, @data-src): //img


<slideshow>: $body//div[has-class("gallery-wrapper")]
<figure>: $@/div[has-class("item")]
@html_to_dom: $@/img/@alt
<figcaption>: $@
@append_to("./ancestor::figure"): $@
@remove: $body//div[has-class("photo__info")]
@remove: $body//div[has-class("stats")]
@remove: $body//hr[has-class("fat")]

#  ___  _                   
# |__ \| |                  
#    ) | |_ _ __ _   _  ___ 
#   / /| __| '__| | | |/ _ \
#  |_| | |_| |  | |_| |  __/
#  (_)  \__|_|   \__,_|\___|

?true

# ULTIMATE PARSER 9000

$problems: $body
@while( $problems ) {
  $test: $body//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or (self::iframe and not(./ancestor::figure)) or (self::video and not(./ancestor::figure)) or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)] or self::h1 or self::h2 or self::h3 or self::h4 or self::h5]
 
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
 
  @if( $problems ) {
    $parent: $problems/parent::*
    @detach: $parent/node()
    @set_attr("merge", $index): $@
    <remove>: $parent
    @after_el("./parent::*"): $problems
    @combine: $body//self::*[prev-sibling::*/@merge = @merge]
  }
}

#?path: /*about/*.*
#?path: /*actions.*
?path: /*projects.*
?path: /*pages/*mobile.*
?path: /*pages/*presentation.*

body!!: null