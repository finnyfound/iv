~version: "2.1"

site_name: "Агентство городских новостей «Москва»"
channel: "@moscownewsagency"
article: //div[has-class("article")]

body: $article/div[has-class("Text")]

@replace("(\\d{2}\\.\\d{2}|^)\\s*(\\d{2}:\\d{2})", "UNKNOWN $2 $1 "): $article/p[has-class("Date")]/text()
@if( "$article/p[has-class(\"Date\")][contains(text(),\"UNKNOWN\")]" ) {
 @replace("UNKNOWN ", ""): $article/p[has-class("Date")]/text()
 @append("0 years ago"): $article/p[has-class("Date")]
 @html_to_dom: $$
 $dom: $@
 @replace("\\s(\\d+)\\.(\\d+)\\s", " $2/$1 ")
 @datetime(+3): $dom
 published_date: $@
}
@datetime(+3, "ru-RU", "dd.MM.y HH:mm"): $article/p[has-class("Date")]
published_date: $@

# Video

<video>: $article//*[has-class("VideoButton")]
$video: $@
@set_attr(src, @href): $video
@prepend_to($body): $video

# Photo gallery

<div>: $article/div[@id="GalleryContainer"]
slideshow
@append_to($body): $slideshow
@match("gallery: (\\[.*)\\s*}\\);", 1): //script[contains(text(), "#GalleryContainer")]
@json_to_xml: $@/text()
@map( $@//item ) {
 $current: $@
 @append(<figure>): $slideshow
 $figure: $@
 @append(<img>): $figure
 @set_attr(src, "https://b1.mskagency.ru/c/", $current/file_id, "\\.jpg"): $@
 @append(<figcaption>): $figure
 @append(<cite>): $@
 $cite
 #@html_to_dom: $current/author_name
 #@append_to($cite): $@
}
image_url: $body//img[1]/@src

#@match("(.*?)\\?.*", 1): $body//video/@src
# Cover

cover: $article/div[@id="PhotoWallList"]//img

# Related
<related>: $article/p[has-class("AnotherMaterials")]
$news: $@
@remove: $news//a
@wrap_inner(<h1>): $news
@repeat( 5 ) {
  @append_to($news): ($article/div[@id="News"]//a)[1]
}
@append_to($body): $news

<related>: $article/p[has-class("RelatedMaterials")]
related: $@
@if( $related ) {
  @remove: $body/related[has-class("AnotherMaterials")]
}
@append(<div>): $related
$omegadiv: $@
@repeat( 5 ) {
  @append_to($omegadiv): ($related/a)[1]
}
@remove: $related/a
@wrap(<h1>): $related/text()
@append_to($body): $related


#  ___  _                   
# |__ \| |                  
#    ) | |_ _ __ _   _  ___ 
#   / /| __| '__| | | |/ _ \
#  |_| | |_| |  | |_| |  __/
#  (_)  \__|_|   \__,_|\___|

?true
@replace("^\\s+$", ""): //text()

# ULTIMATE PARSER 9000

$problems: $body
@while( $problems ) {
  $test: $body//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or self::iframe or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)]]
 
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
 
  @if( $problems ) {
    $parent: $problems/parent::*
    @detach: $parent/node()
    @set_attr("merge", $index): $@
    <remove>: $parent
    @after_el("./parent::*"): $problems
    @combine: $body//self::*[prev-sibling::*/@merge = @merge]
  }
}